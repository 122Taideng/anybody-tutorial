/* ----------------------------------------------------------------------------------
This is a 2-D bicycle model. It is a slightly simplified version of the official
Bike2D model in the AnyScript repository, and it is adapted for use in the
muscle recruitment tutorial.

The model of the bike which includes bike frame, crank, rear wheel, definition of 
crank moment etc. can be found in the include file "BikeFrameAndWheels.any" in 
this directory.

The connections between the "human model" Leg2D, to the bikeframe 
are defined in the include file BikeLegConnection.any.
---------------------------------------------------------------------------------- */

Main = {
  
  AnyFolder BikeParameters = {
    
    // Geometry parameters
    AnyVar PedalArmLength =0.17;  //Length of pedal arm
    AnyVar PedalArmWidth = 0.106; //Horizontal distance between left and right connecting point between foot and pedal
    AnyVar SaddleHeight = 0.63;  //Height of hip joint measured vertically from the crank
    AnyVar SaddlePos = -0.15;     //Horizontal pos of hipjoint measured from the crank
    
    // Kinematic parameters
    AnyVar Cadence = 60.0;   //Cadence in RPM 
    AnyVar MechOutput = 250; //Average Mechanical output over a cycle in Watt
    AnyVar PhaseShift = atan(SaddlePos/SaddleHeight); // Phase shift for different seat positions
    AnyVar WigglePhase = 0.60-PhaseShift; // Phase shift for different seat positions
    AnyVar FootWiggleAmp = 20*pi/180;  // Amplitude of ankle dorsi/plantar flextion w.r.t. ground
    AnyVar FootWiggleOffs = -23*pi/180;  // Offset of dorsi/plantar flextion
    
    // The function for the crank moment is defined as Moment=Offset-Amp*cos(4*pi*t/T+Phase)
    AnyVar T = 60/Cadence; //Cycle time
    
    // Loading parameters
    AnyVar CrankMomentTopDeadCenter = 3.0; // This is the moment which can be applied by the rider in the top dead center point
    AnyVar CrankMomentOffset = (MechOutput*T)/(2*pi); 
    AnyVar CrankMomentAmp = CrankMomentOffset-CrankMomentTopDeadCenter;
    AnyVar CrankMomentPhase = -PhaseShift;    
  };
    
  // These are the attachment positions of the bike frames to the global reference frame
  AnyFixedRefFrame GlobalRef = {
  };  // Global reference frame  
    
  // Body parameters that the leg model will need a reference to.
  AnyFolder BodyParameters = {
    AnyVar BodyMass = 75;
    AnyVar Density = 1000;
  }; //Bodyparameters
  
  #include "DrawSettings.any"
  #include "Scaling.any"
  
  // This folder assembles all elements to be included in the study
  AnyFolder Bike2D = {
    
    AnyFolder Model = {
      
      // Import the 2-D leg from the repository
      #include "Leg2D.any"
      AnyFolder &HumanFolder = Leg2D;  

      // Include the environment, i.e. the bicycle
      #include "BikeFrameAndWheels.any"

      // Include the specification of joints between the human model
      // and the bicycle
      #include "BikeLegConnection.any"        
      AnyRefFrame& GroundNode = Main.GlobalRef;
      
      // Include specification of the joints between the bicycle and the ground
      #include "BikeFrameGroundConnection.any"
    };
    
    // The study: Operations to be performed on the model
    AnyBodyStudy Study = {      
      AnyFolder &Model = .Model;
      Gravity = {0.0, -9.81, 0.0};
      tEnd = Main.BikeParameters.T;
      nStep = 100;
      InverseDynamics.Criterion = {
        Type = MR_MinMaxStrict;
//        AuxQuadraticTerm.Weight = 0;
      };
    };  
    
  }; //Bike2D
    
}; // Main